# SPDX-FileCopyrightText: 2020 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

if (current_cpu == "x86" || current_cpu == "x86_64") {
  declare_args() {
    # Build file to import for X86 defaults.
    x86_platform_config = ""
  }

  # Allow platforms to override how ARM architecture flags are chosen by
  # providing a file to import.
  if (x86_platform_config != "") {
    _platform_defaults = {
      import(x86_platform_config)
    }
  }

  _defaults = {
    x86_arch = ""
    x86_cpu = ""
    x86_tune = ""

    # Update defaults with platform values, if any.
    if (x86_platform_config != "") {
      forward_variables_from(_platform_defaults, "*")
    }
  }

  declare_args() {
    # ARM architecture (value for -march flag).
    x86_arch = _defaults.x86_arch

    # ARM CPU (value for -mcpu flag).
    x86_cpu = _defaults.x86_cpu

    # ARM tuning (value for -mtune flag).
    x86_tune = _defaults.x86_tune
  }
}
