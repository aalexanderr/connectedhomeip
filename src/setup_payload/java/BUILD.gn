# SPDX-FileCopyrightText: (c) 2020 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")

import("${build_root}/config/android_abi.gni")
import("${chip_root}/build/chip/java/rules.gni")

if (defined(build_java_matter_controller)) {
  build_java_matter_controller = build_java_matter_controller
} else {
  build_java_matter_controller = false
}

shared_library("jni") {
  output_name = "libSetupPayloadParser"

  sources = [ "SetupPayloadParser-JNI.cpp" ]

  deps = [
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload",
  ]

  output_dir = "${root_out_dir}/lib/jni/${android_abi}"
}

android_library("java") {
  output_name = "SetupPayloadParser.jar"

  data_deps = [ ":jni" ]

  if (!build_java_matter_controller) {
    data_deps += [ "${chip_root}/build/chip/java:shared_cpplib" ]
  }

  sources = [
    "src/chip/setuppayload/DiscoveryCapability.java",
    "src/chip/setuppayload/OptionalQRCodeInfo.java",
    "src/chip/setuppayload/SetupPayload.java",
    "src/chip/setuppayload/SetupPayloadParser.java",
  ]

  javac_flags = [ "-Xlint:deprecation" ]

  # TODO: add classpath support (we likely need to add something like
  #  ..../platforms/android-21/android.jar to access BLE items)
}
