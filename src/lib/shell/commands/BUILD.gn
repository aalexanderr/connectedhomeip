# SPDX-FileCopyrightText: 2021 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

import("//build_overrides/chip.gni")

import("${chip_root}/src/lib/core/core.gni")
import("${chip_root}/src/platform/device.gni")
import("${chip_root}/src/system/system.gni")

source_set("commands") {
  sources = [
    "Base64.cpp",
    "Help.cpp",
    "Help.h",
    "Meta.cpp",
  ]

  public_deps = [ "${chip_root}/src/lib/shell:shell_core" ]

  if (chip_device_platform != "none") {
    sources += [
      "Config.cpp",
      "Device.cpp",
      "OnboardingCodes.cpp",
    ]
  }

  if (chip_enable_wifi) {
    sources += [ "WiFi.cpp" ]
  }

  if (chip_enable_ble && chip_device_platform != "none") {
    sources += [ "BLE.cpp" ]
  }

  if (chip_enable_nfc && chip_device_platform != "none") {
    sources += [ "NFC.cpp" ]
  }

  if (chip_device_platform != "none") {
    sources += [ "Dns.cpp" ]
  }

  if (chip_enable_ota_requestor && chip_device_platform != "none" &&
      chip_device_platform != "linux" && chip_device_platform != "darwin") {
    sources += [ "Ota.cpp" ]

    # TODO: the ota commands in shell should use the interfaces in /src/app/clusters/ota-requestor
    #
    # Awkward dependencies below say:
    #   - codegen via zap the CHIPClusters.h (which is the CLIENT SIDE)
    #   - ensure include path can see zap-generated/CHIPClusters.h
    deps = [ "${chip_root}/src/controller/data_model:data_model_zapgen_files" ]

    configs +=
        [ "${chip_root}/src/controller/data_model:data_model_zapgen_config" ]
  }

  if (chip_system_config_provide_statistics) {
    sources += [ "Stat.cpp" ]
  }

  if (chip_device_platform != "none") {
    public_deps += [ "${chip_root}/src/app/server" ]
  }

  cflags = [ "-Wconversion" ]
}
