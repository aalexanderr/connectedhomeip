# SPDX-FileCopyrightText: 2020 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

import("//build_overrides/chip.gni")
import("//build_overrides/nlassert.gni")
import("${chip_root}/build/chip/buildconfig_header.gni")
import("${chip_root}/src/crypto/crypto.gni")
import("${chip_root}/src/platform/nxp/crypto/se05x/args.gni")

if (chip_crypto == "platform") {
  import("//build_overrides/mbedtls.gni")
}

source_set("public_headers") {
  sources = [
    "CHIPCryptoPAL.h",
    "OperationalKeystore.h",
  ]

  public_deps = [
    "${chip_root}/src/lib/asn1",
    "${chip_root}/src/lib/core",
    "${chip_root}/src/lib/support",
    "${nlassert_root}:nlassert",
  ]
}

static_library("nxp_crypto_lib") {
  sources = [
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPALHost.cpp",
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPALHsm_se05x_hkdf.cpp",
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPALHsm_se05x_hmac.cpp",
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPALHsm_se05x_p256.cpp",
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPALHsm_se05x_pbkdf.cpp",
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPALHsm_se05x_rng.cpp",
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPALHsm_se05x_utils.cpp",
    "${chip_root}/src/platform/nxp/crypto/se05x/CHIPCryptoPAL_HostFallBack.cpp",
  ]

  public_deps = [ ":public_headers" ]
  deps = [ "${chip_root}/${nxp_crypto_mw_root}:se05x" ]

  external_mbedtls = current_os == "zephyr"

  if (!external_mbedtls) {
    public_deps += [ "${mbedtls_root}:mbedtls" ]
  }

  include_dirs = [
    ".",
    "${chip_root}/src/crypto",
    "${chip_root}/src/platform/nxp/crypto/se05x",
  ]
}
