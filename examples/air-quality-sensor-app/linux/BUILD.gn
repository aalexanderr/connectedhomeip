# SPDX-FileCopyrightText: 2020 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0

import("//build_overrides/chip.gni")

import("${chip_root}/build/chip/tools.gni")
import("${chip_root}/src/app/common_flags.gni")
import("${chip_root}/third_party/imgui/imgui.gni")

assert(chip_build_tools)

config("includes") {
  include_dirs = [
    ".",
    "include",
  ]
}

executable("air-quality-sensor-app") {
  sources = [
    "AirQualitySensorAppAttrUpdateDelegate.cpp",
    "include/CHIPProjectAppConfig.h",
    "main.cpp",
  ]

  deps = [
    "${chip_root}/examples/air-quality-sensor-app/air-quality-sensor-common",
    "${chip_root}/examples/platform/linux:app-main",
    "${chip_root}/src/lib",
    "${chip_root}/third_party/jsoncpp",
  ]

  if (chip_examples_enable_imgui_ui) {
    deps += [
      "${chip_root}/examples/common/imgui_ui",
      "${chip_root}/examples/common/imgui_ui/windows:boolean_state",
      "${chip_root}/examples/common/imgui_ui/windows:occupancy_sensing",
      "${chip_root}/examples/common/imgui_ui/windows:qrcode",
    ]
  }

  include_dirs = [ "include" ]

  cflags = [ "-Wconversion" ]

  output_dir = root_out_dir
}

group("linux") {
  deps = [ ":air-quality-sensor-app" ]
}

group("default") {
  deps = [ ":linux" ]
}
