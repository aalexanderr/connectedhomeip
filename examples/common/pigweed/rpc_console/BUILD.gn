# SPDX-FileCopyrightText: (c) 2021 Project CHIP Authors
#
# SPDX-License-Identifier: Apache-2.0
import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")
import("$dir_pw_build/python_dist.gni")

group("default") {
  deps = [
    ":chip_rpc_distribution.wheel",
    ":pip_install_editable_chip_rpc",
    "py:chip_rpc._build_wheel",
    "py:chip_rpc_wheel",
  ]
}

# This target creates a single Python package and wheel for chip-rpc. It will
# merge all Python dependencies from Pigweed and Matter. The output is located
# in:
#   out/obj/chip_rpc_distribution/  <- source files here
#   out/obj/chip_rpc_distribution._build_wheel/chip_rpc-0.0.1-py3-none-any.whl
pw_create_python_source_tree("chip_rpc_distribution") {
  packages = [ "py:chip_rpc" ]
  generate_setup_cfg = {
    common_config_file = "common_setup.cfg"
    append_date_to_version = true
  }
  extra_files = [ "py/pyproject.toml > pyproject.toml" ]
}

pw_internal_pip_install("pip_install_editable_chip_rpc") {
  packages = [ "py:chip_rpc" ]
  editable = true
}
